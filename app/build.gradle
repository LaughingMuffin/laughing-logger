plugins {
    id 'com.android.application'

    // Add the Google services Gradle plugin
    id 'com.google.gms.google-services'
}

android {
    namespace 'org.laughing.logger'
    compileSdk 34

    defaultConfig {
        applicationId "org.laughing.logger"
        minSdkVersion 21
        targetSdkVersion 34
        versionName '3.0.0'
        versionCode 300

        multiDexEnabled true

        testApplicationId "org.laughing.logger.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [appName: "@string/app_name"]

        vectorDrawables.useSupportLibrary = true
    }

    if (project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS_LL
                keyPassword RELEASE_KEY_PASSWORD_LL
            }
        }
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            manifestPlaceholders = [appName: "Laughing Logger DEBUG"]
        }
    }

    flavorDimensions "store"
    productFlavors {
        play {
            dimension "store"
        }
        fdroid {
            dimension "store"
            applicationId "org.laughing.loggerlibre"
            manifestPlaceholders = [appName: "@string/app_name_libre"]
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    // do not upgrade - fastscroll will not work
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation "androidx.recyclerview:recyclerview:1.3.2"
    implementation 'androidx.annotation:annotation:1.7.0'

    // do not upgrade - fastscroll will not work
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'me.zhanghai.android.fastscroll:library:1.1.7'

    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'

    playImplementation platform('com.google.firebase:firebase-bom:32.6.0')
    playImplementation("com.google.firebase:firebase-crashlytics")
    playImplementation("com.google.firebase:firebase-analytics")

    // do not upgrade (for now)
    implementation 'com.mikhaellopez:circularprogressbar:2.0.0'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'